apply plugin: "lesscss"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'edu.sc.seis.launch4j'

dependencies {
    compile project(':network')

    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'

    compile group: 'io.netty', name: 'netty-buffer', version: '4.1.21.Final'
    compile group: 'io.netty', name: 'netty-codec', version: '4.1.21.Final'
    compile group: 'io.netty', name: 'netty-handler', version: '4.1.21.Final'
    compile group: 'io.netty', name: 'netty-transport', version: '4.1.21.Final'
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/resources/"]
        }
    }
}

jar {
    from("${buildDir}") {
        include 'style/*.css'
    }

    manifest {
        attributes(
                'Main-Class': "tv.dotstart.minecraft.clock.MineClockApplication",
                'Implementation-Title': project.name,
                'Implementation-Vendor-Id': project.group,
                'Implementation-Version': project.version
        )
    }

    dependsOn 'lesscss'
}

lesscss {
    source = fileTree('src/main/less') {
        include '*.less'
    }
    dest = "${buildDir}/style/"
    compress = true
}

shadowJar {
    baseName = 'MineClock'
    classifier = null
    version = null
    configurations = [project.configurations.compile]

    from("${buildDir}") {
        include 'style/*.css'
    }

    manifest {
        inheritFrom project.tasks.jar.manifest
    }
}

launch4j {
    outfile = "MineClock.exe"
    mainClassName = "tv.dotstart.minecraft.clock.MineClockApplication"
    jar = "${projectDir}/build/libs/${project.tasks.shadowJar.baseName}.jar"
    icon = "${projectDir}/src/main/resources/image/application.ico"

    copyConfigurable = project.tasks.shadowJar.outputs.files
}
